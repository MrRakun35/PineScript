// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Mr.Rakun

//@version=5
indicator("RSI with TA", shorttitle="RSI with TA")

// RSI hesaplaması
rsiLength = input(14, title="RSI Length")
rsiSource = input(close, title="RSI Source")
rsi = ta.rsi(rsiSource, rsiLength)
showDivergence = input.bool(false, title="Show Divergence", group="RSI Settings", display = display.data_window)

// RSI mum çubuğu oluşturma
openRsi = rsi[1]  // Önceki RSI değeri
closeRsi = rsi  // Mevcut RSI değeri
highRsi = math.max(openRsi, closeRsi)  // Yüksek RSI değeri
lowRsi = math.min(openRsi, closeRsi)  // Düşük RSI değeri

// RSI mum çubuğunu çizme
plotcandle(openRsi, highRsi, lowRsi, closeRsi, title="RSI Candlestick", color=(closeRsi >= openRsi ? color.green : color.red))

rsiUpperBand = hline(70, "RSI Upper Band", color=#787B86)
midline = hline(50, "RSI Middle Band", color=color.new(#787B86, 50))
rsiLowerBand = hline(30, "RSI Lower Band", color=#787B86)
fill(rsiUpperBand, rsiLowerBand, color=color.rgb(126, 87, 194, 90), title="RSI Background Fill")

// Göstergeler için seçim kutuları
showMA = input(false, title="Show Moving Average")
showDonchian = input(false, title="Show Donchian Channel")


// Donchian Kanalı hesaplaması
donchianLength = input(20, title="Donchian Channel Length")
donchianHigh = ta.highest(highRsi, donchianLength)
donchianLow = ta.lowest(lowRsi, donchianLength)
donchianMid = (donchianHigh + donchianLow) / 2

// Donchian Kanalı'nı çizme
plot(donchianHigh, title="Donchian High", color= showDonchian ? color.green: na)
plot(donchianLow, title="Donchian Low", color= showDonchian ? color.red: na)
plot(donchianMid, title="Donchian Mid", color= showDonchian ? color.gray: na)

// Hareketli Ortalama
maLength = input(50, title="Moving Average Length")
ma = ta.sma(rsi, maLength)

plot(ma, title="Moving Average", color= showMA ? color.orange: na)

// Divergence
lookbackRight = 5
lookbackLeft = 5
rangeUpper = 60
rangeLower = 5
bearColor = color.red
bullColor = color.green
textColor = color.white
noneColor = color.new(color.white, 100)

plFound = na(ta.pivotlow(rsi, lookbackLeft, lookbackRight)) ? false : true
phFound = na(ta.pivothigh(rsi, lookbackLeft, lookbackRight)) ? false : true
_inRange(cond) =>
	bars = ta.barssince(cond == true)
	rangeLower <= bars and bars <= rangeUpper

//------------------------------------------------------------------------------
// Regular Bullish
// rsi: Higher Low

rsiHL = rsi[lookbackRight] > ta.valuewhen(plFound, rsi[lookbackRight], 1) and _inRange(plFound[1])

// Price: Lower Low

priceLL = low[lookbackRight] < ta.valuewhen(plFound, low[lookbackRight], 1)
bullCondAlert = priceLL and rsiHL and plFound
bullCond = showDivergence and bullCondAlert

plot(
     plFound ? rsi[lookbackRight] : na,
     offset=-lookbackRight,
     title="Regular Bullish",
     linewidth=2,
     color=(bullCond ? bullColor : noneColor),
	 display = display.pane
     )

plotshape(
	 bullCond ? rsi[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Regular Bullish Label",
	 text=" Bull ",
	 style=shape.labelup,
	 location=location.absolute,
	 color=bullColor,
	 textcolor=textColor
	 )

//------------------------------------------------------------------------------
// Regular Bearish
// rsi: Lower High

rsiLH = rsi[lookbackRight] < ta.valuewhen(phFound, rsi[lookbackRight], 1) and _inRange(phFound[1])

// Price: Higher High

priceHH = high[lookbackRight] > ta.valuewhen(phFound, high[lookbackRight], 1)

bearCondAlert = priceHH and rsiLH and phFound
bearCond = showDivergence and bearCondAlert

plot(
	 phFound ? rsi[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Regular Bearish",
	 linewidth=2,
	 color=(bearCond ? bearColor : noneColor),
	 display = display.pane
	 )

plotshape(
	 bearCond ? rsi[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Regular Bearish Label",
	 text=" Bear ",
	 style=shape.labeldown,
	 location=location.absolute,
	 color=bearColor,
	 textcolor=textColor
	 )

alertcondition(bullCondAlert, title='Regular Bullish Divergence', message="Found a new Regular Bullish Divergence, `Pivot Lookback Right` number of bars to the left of the current bar.")
alertcondition(bearCondAlert, title='Regular Bearish Divergence', message='Found a new Regular Bearish Divergence, `Pivot Lookback Right` number of bars to the left of the current bar.')
